buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
    }
}

version "0.1-SNAPSHOT"
group "nl.thehyve"

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'org.grails.grails-web'
apply plugin: 'net.linguica.maven-settings'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

ext {
    guavaVersion = '30.0-jre'
    hibernateCoreVersion = '5.2.18.Final'
    jacksonVersion = '2.11.0'
    keycloakVersion = '12.0.4'
    mailPluginVersion = '2.0.0'
    postgresqlVersion = '42.2.14.jre7'
    quartzPluginVersion = '2.0.13'
    quartzVersion = '2.3.2'
    springSecurityVersion = '4.2.17.RELEASE'
    tomcatVersion = '8.5.63'
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:${grailsVersion}"
        mavenBom "org.keycloak.bom:keycloak-adapter-bom:${keycloakVersion}"
    }
    applyMavenExclusions false
}

dependencies {
    profile "org.grails.profiles:rest-api"

    console("org.grails:grails-console") {
        exclude group: 'org.fusesource.jansi', module: 'jansi'
    }

    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.google.guava:guava:${guavaVersion}"
    compile "org.apache.tomcat:tomcat-annotations-api:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}"
    compile "org.grails:grails-core"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-codecs"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-datasource"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5"
    compile "org.grails.plugins:mail:${mailPluginVersion}"
    compile "org.grails.plugins:views-json"
    compile "org.grails.plugins:views-json-templates"
    compile ("org.grails.plugins:quartz:${quartzPluginVersion}") {
        exclude group: 'org.quartz-scheduler', module: 'quartz'
    }
    compile("org.keycloak:keycloak-legacy-spring-boot-starter") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile("org.springframework.boot:spring-boot-starter-actuator") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.springframework.security:spring-security-core:${springSecurityVersion}"
    compile "org.springframework.security:spring-security-web:${springSecurityVersion}"
    compile "org.hibernate:hibernate-core:${hibernateCoreVersion}"
    compile "org.quartz-scheduler:quartz:${quartzVersion}"

    runtime "com.h2database:h2"
    runtime "org.apache.tomcat:tomcat-jdbc"
    runtime "org.postgresql:postgresql:${postgresqlVersion}"

    testCompile "org.grails:grails-test-mixins:3.3.0"
    testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
}

publishing {
    repositories {
        maven {
            name 'nl.thehyve.nexus'
            url 'https://repo.thehyve.nl/content/repositories/snapshots/'
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifact file("${libsDir}/${project.name}-${project.version}.war")
            artifactId 'gb-backend'

        }
    }
}
publish.dependsOn 'bootRepackage'
publishToMavenLocal.dependsOn 'bootRepackage'

bootRun {
    jvmArgs = ['-Dspring.output.ansi.enabled=always']
}

jacocoTestReport {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport
